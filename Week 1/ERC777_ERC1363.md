
# ERC777: Improving on the ERC-20 Token Standard

If you are new to *crypto*, seeing that your *swap* button disabled is very odd:

![alt text](https://dappgrid.com/wp-content/uploads/2021/04/pancakeswap-exchange-tokens.jpg)

That is because for ERC20, Users must understand that transferring tokens on behalf of another account involves a two-step process.

1) The token holder must call the `approve` function to authorize another spender, such as *PancakeSwap*.
2) The token holder must then initiate another transaction to swap it, where the PancakeSwap smart contract can then call the `transferFrom` function to transfer tokens up to the amount the token holder had previously approved.

*ERC777 aims to improve these UX issues, as well as give Developers more room for custom logic.*

# Let's break down on how ERC777 transfers tokens

## There are 5 important parts to this workflow

1. Sender (EOA/Smart contract)
2. Operators (address of EOAs/Smart contracts/dApps)
3. ERC777 (Smart contract)
4. ERC1820 Registry Contract (Smart contract)
5. Recipient (EOA/Smart contract)


## 1. The Sender

In order to make use of the Hooks defined by ERC777, the Sender and Recipient have to be **Smart Contracts**.

Your typical EOAs (Externally Owned Accounts), the one you use MetaMask to interact with, doesn't have any executable code to implement ERC777 hooks.

To implement the `tokensToSend` hook, your Sender smart contract must:

1. Implement `IERC777Sender`
2. `registered with the ERC20 registry smart contract` This will be important when we reach the *Registry Service Layer* in part 3.

```sol
interface IERC777Sender {
    /**
     * @dev Called by an {IERC777} token contract whenever a registered holder's
     * (`from`) tokens are about to be moved or destroyed. The type of operation
     * is conveyed by `to` being the zero address or not.
     *
     * This call occurs _before_ the token contract's state is updated, so
     * {IERC777-balanceOf}, etc., can be used to query the pre-operation state.
     *
     * This function may revert to prevent the operation from being executed.
     */
    function tokensToSend(
        address operator,
        address from,
        address to,
        uint256 amount,
        bytes calldata userData,
        bytes calldata operatorData
    ) external;
}
```

```sol


interface IERC1820Registry {
    function setInterfaceImplementer(address account, bytes32 interfaceHash, address implementer) external;
}

contract YourSenderContract is IERC777Sender {
    IERC1820Registry private _erc1820Registry;
    bytes32 private constant _ERC777_SENDER_INTERFACE_HASH = keccak256("ERC777TokensSender");

    constructor(address erc1820RegistryAddress) {
        _erc1820Registry = IERC1820Registry(erc1820RegistryAddress);
    }

    function register() external {
        _erc1820Registry.setInterfaceImplementer(address(this), _ERC777_SENDER_INTERFACE_HASH, address(this));
    }

    // ... rest of the contract ...
}

```
## 2. Operators

Let's assume we want *PancakeSwap* to be an approved operator.

Instead of having to approve every transaction we make on *PancakeSwap*, we can add *PancakeSwap's* Contract Address as an `operator` for your Sender smart contract.

Snippet from Openzeppelin's ERC777.sol
```sol
 // This isn't ever read from - it's only used to respond to the defaultOperators query.
    address[] private _defaultOperatorsArray;

    // Immutable, but accounts may revoke them (tracked in __revokedDefaultOperators).
    mapping(address => bool) private _defaultOperators;

    // For each account, a mapping of its operators and revoked default operators.
    mapping(address => mapping(address => bool)) private _operators;
    mapping(address => mapping(address => bool)) private _revokedDefaultOperators;
```
Here are two ways to get into/out of the operators list

1. calling `authorizeOperator` and `revokeOperator` on the ERC777 Token Contract.
2. adding an address `defaultOperators` in the ERC777 Token Contract on deployment

A simple example for option 1:
```sol 
pragma solidity ^0.8.0;

contract SenderContract {
    
    IERC777 private erc777Token;
    
    constructor(address _erc777TokenAddress) {
        erc777Token = IERC777(_erc777TokenAddress);
    }
    
    function authorizeOperatorInTokenContract(address operator) external {
        erc777Token.authorizeOperator(operator);
    }
}
```

Remember that in this example,the `msg.sender` for the `authorizeOperator` call will be the address of the `SenderContract`, not the original caller from EOA.

## 3. ERC777 

Consider the `_send` and `operatorSend` function below:

```sol
/**
     * @dev See {IERC777-operatorSend}.
     *
     * Emits {Sent} and {IERC20-Transfer} events.
     */
    function operatorSend(
        address sender,
        address recipient,
        uint256 amount,
        bytes memory data,
        bytes memory operatorData
    ) public virtual override {
        require(isOperatorFor(_msgSender(), sender), "ERC777: caller is not an operator for holder");
        _send(sender, recipient, amount, data, operatorData, true);
    }
/**
     * @dev Send tokens
     * @param from address token holder address
     * @param to address recipient address
     * @param amount uint256 amount of tokens to transfer
     * @param userData bytes extra information provided by the token holder (if any)
     * @param operatorData bytes extra information provided by the operator (if any)
     * @param requireReceptionAck if true, contract recipients are required to implement ERC777TokensRecipient
     */
    function _send(
        address from,
        address to,
        uint256 amount,
        bytes memory userData,
        bytes memory operatorData,
        bool requireReceptionAck
    ) internal virtual {
        require(from != address(0), "ERC777: transfer from the zero address");
        require(to != address(0), "ERC777: transfer to the zero address");

        address operator = _msgSender();

        _callTokensToSend(operator, from, to, amount, userData, operatorData);

        _move(operator, from, to, amount, userData, operatorData);

        _callTokensReceived(operator, from, to, amount, userData, operatorData, requireReceptionAck);
    }
```
Breakdown:

1. `_callTokensToSend(operator, from, to, amount, userData, operatorData);`

```sol
/**
     * @dev Call from.tokensToSend() if the interface is registered
     * @param operator address operator requesting the transfer
     * @param from address token holder address
     * @param to address recipient address
     * @param amount uint256 amount of tokens to transfer
     * @param userData bytes extra information provided by the token holder (if any)
     * @param operatorData bytes extra information provided by the operator (if any)
     */
    function _callTokensToSend(
        address operator,
        address from,
        address to,
        uint256 amount,
        bytes memory userData,
        bytes memory operatorData
    ) private {
        address implementer = _ERC1820_REGISTRY.getInterfaceImplementer(from, _TOKENS_SENDER_INTERFACE_HASH);
        if (implementer != address(0)) {
            IERC777Sender(implementer).tokensToSend(operator, from, to, amount, userData, operatorData);
        }
    }
```
`IERC1820Registry internal constant _ERC1820_REGISTRY = IERC1820Registry(0x1820a4B7618BdE71Dce8cdc73aAB6C95905faD24);`

- It first checks the IERC1820 Registry Contract if the *Sender Contract* has been `registered` with the`tokenToSend` function. 
- If it does, implementer would NOT be `address(0)`.Then it will call the `tokensToSend` hook in the Sender smart contract. 

2. `_move(operator, from, to, amount, userData, operatorData);`

```sol
function _move(
        address operator,
        address from,
        address to,
        uint256 amount,
        bytes memory userData,
        bytes memory operatorData
    ) private {
        _beforeTokenTransfer(operator, from, to, amount);

        uint256 fromBalance = _balances[from];
        require(fromBalance >= amount, "ERC777: transfer amount exceeds balance");
        unchecked {
            _balances[from] = fromBalance - amount;
        }
        _balances[to] += amount;

        emit Sent(operator, from, to, amount, userData, operatorData);
        emit Transfer(from, to, amount);
    }
```
- `_beforeTokenTransfer` is another hook that is called by the implementing ERC777 token contract before any token transfer
- Updates balances
- Emits `Sent` and `Transfer` event


3. `_callTokensReceived`

```sol
/**
     * @dev Call to.tokensReceived() if the interface is registered. Reverts if the recipient is a contract but
     * tokensReceived() was not registered for the recipient
     * @param operator address operator requesting the transfer
     * @param from address token holder address
     * @param to address recipient address
     * @param amount uint256 amount of tokens to transfer
     * @param userData bytes extra information provided by the token holder (if any)
     * @param operatorData bytes extra information provided by the operator (if any)
     * @param requireReceptionAck if true, contract recipients are required to implement ERC777TokensRecipient
     */
    function _callTokensReceived(
        address operator,
        address from,
        address to,
        uint256 amount,
        bytes memory userData,
        bytes memory operatorData,
        bool requireReceptionAck
    ) private {
        address implementer = _ERC1820_REGISTRY.getInterfaceImplementer(to, _TOKENS_RECIPIENT_INTERFACE_HASH);
        if (implementer != address(0)) {
            IERC777Recipient(implementer).tokensReceived(operator, from, to, amount, userData, operatorData);
        } else if (requireReceptionAck) {
            require(!to.isContract(), "ERC777: token recipient contract has no implementer for ERC777TokensRecipient");
        }
    }
```

- This is similar to `_callTokensToSend` with a very important addition
- Because developers can make mistakes when writing smart contracts, the `else if` block is a safety mechanism that would revert the transaction if the receiving smart contract did not implement the `tokensReceived` hook and has not `registered`  with the ERC1820 contract with the `IERC777Recipient` interface.
- But if the receiver is your typical EOA, it would not revert as the hook would not be called, and the transfer would proceed as usual.


## 4. ERC1820 Registry Contract

The ERC1820 Registry Contract is a shared, global contract that is used for managing interface implementations for any contract, not just ERC777. It was introduced in EIP-1820 as a standard way for contracts to publish and discover information about the interfaces they implement. This allows smart contracts to interact with one another in a more flexible and efficient way.

Think of it as a public utility smart contract.


From the ERC1820
```sol
    
    /// @notice mapping from addresses and interface hashes to their implementers.
    mapping(address => mapping(bytes32 => address)) internal interfaces;

```

The Sender and/or Receiver smart contract, after implementing IERC777Sender and/or IERC777Recipient, must register with the ERC1820 smart contract to populate the `interfaces` mapping.

```sol
contract MyContract is IERC777Sender, IERC777Recipient {
    ERC1820Registry private registry;
    
    constructor(address _ERC1820RegistryAddress) {
        registry = ERC1820Registry(_ERC1820RegistryAddress);
        bytes32 senderInterfaceHash = keccak256("ERC777TokensSender");
        bytes32 recipientInterfaceHash = keccak256("ERC777TokensRecipient");
        
        registry.setInterfaceImplementer(address(this), senderInterfaceHash, address(this));
        registry.setInterfaceImplementer(address(this), recipientInterfaceHash, address(this));
    }
    
    // Implement the tokensToSend and tokensReceived functions as per IERC777Sender and IERC777Recipient interfaces
}

```


## The Recipient

To implement the `tokensReceived` hook, the Recipient smart contract must be a smart contract implements `IERC777Recipient` and has registered with ERC1820 as explained in previous parts.

The Recipient smart contract's `tokensReceived` function upon receiving the tokens.

# What are the issues with the ERC777?

## ERC1363 - The "Payable Token": Why was it introduced?

Let's first consider the `payable` modifier in Solidity. 

Functions need to be marked `payable` to receive ether, otherwise the transaction will `revert`.

What if we wanted to pay in tokens, instead of ether? One of the reasons for that is to avoid Ether price volatility.

## There are 5 important parts to this workflow


1. The Sender (does not need to be smart contract)
2. The ERC1363 Token
3. The Recipient Smart contract that implements the `IERC1363Receiver` and/or `IERC1363Spender` (Let's call this Marketplace Contract)


## The Sender

This time the, Sender does not need to be a smart contract. Assume that the Sender wants to spend tokens in an E-commerce smart contract. Sender will send it to the Marketplace Contract using the `transferAndCall` method 


## The ERC1363 Token 

The ERC1363 token uses ERC-20 functions like `transfer` and `approve`, but enhances it with additional functions like `transferAndCall`, `transferFromAndCall`, `approveAndCall`. 

When the Sender calls `transferAndCall` method on the ERC1363 token contract, specifying the Marketplace address (Recipient), the token contract will facilitate the transfer, and callbacks if implemented. 

Examples of additionalcustom implementation would be:

1. Add additional bytes as a data payload to Recipient smart contract before triggering `onTransferReceived`
2. Combined Tranfer and Function call, without paying double gas for `approve` and `transfer`
3. It is where the callbacks to receiver/spender is called when tokens are transfered

## Marketplace contract (Recipient)

Consider the `_checkOnTransferReceived` function
```sol
 /**
     * @dev Private function to invoke {IERC1363Receiver-onTransferReceived} on a target address.
     * This will revert if the recipient doesn't accept the token transfer or if the target address is not a contract.
     * @param sender address Representing the previous owner of the given token amount
     * @param recipient address Target address that will receive the tokens
     * @param amount uint256 The amount mount of tokens to be transferred
     * @param data bytes Optional data to send along with the call
     */
    function _checkOnTransferReceived(address sender, address recipient, uint256 amount, bytes memory data) private {
        if (recipient.code.length == 0) {
            revert("ERC1363: transfer to non contract address");
        }

        try IERC1363Receiver(recipient).onTransferReceived(_msgSender(), sender, amount, data) returns (bytes4 retval) {
            require(retval == IERC1363Receiver.onTransferReceived.selector, "ERC1363: receiver returned wrong data");
        } catch (bytes memory reason) {
            if (reason.length == 0) {
                revert("ERC1363: transfer to non ERC1363Receiver implementer");
            } else {
                /// @solidity memory-safe-assembly
                assembly {
                    revert(add(32, reason), mload(reason))
                }
            }
        }
    }
```
When tokens are transferred to the Recipient smart contract using `transferAndCall` or `transferFromAndCall`, the Recipient contract is expected to implement and return the correct function selector for `onTransferReceived`. The entire signature (function name and parameter types must match).

Remember in ERC777, we check the ERC1820 Registry layer if the `IERC777Sender` or `IERC777Recipient` has been implemented and registered? This is similar, only that this is a mechanism for checking contract "interfaces" or "capabilities" in a lightweight manner. Instead of registering to a Registry Layer, we simply check that the Recipient contract has the correct function signatures (For `onTransferReceived` and/or `onApprovalReceived`.

The token contract then attempts to call the `onTransferReceived` function on the Recipient contract. 

# What are the problems with ERC777?


1. Reentrancy using `tokensReceived` hook. A classic way is by designing a Malicious ERC777 contract and implement the `tokensReceived` function that causes the reentrancy attack.

```sol
//Target Exchange contract
function buyTokens(uint256 amount) external {
        require(etherBalance[msg.sender] >= amount, "Not enough ether");

        // Decrease the user's ether balance
        etherBalance[msg.sender] -= amount;

        // Send the equivalent tokens to the user
        IERC777(tokenAddress).send(msg.sender, amount, "");
    }
```

```sol
//Malicious tokensReceived
//This is essentially double spending the 1 ether to buy 2 ethers worth of tokens
function tokensReceived(address, uint256, bytes calldata) external {
        require(msg.sender == exchangeAddress, "Not from exchange");
        Exchange(exchangeAddress).buyTokens(1 ether);
    }
```

2. Violation of the `Check-Effects-Interactions` pattern. As with the above stated reentrancy attack, it also shows that a malicious contract can call the `tokensReceived` hook before updating the tokens balances.



